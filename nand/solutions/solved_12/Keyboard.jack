// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    // static String inputBuffer;

    /** Initializes the keyboard. */
    function void init() {
        // Nothing to do; we allocate a new String for each input
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var char pressed, c;

        // Wait for a key to be pressed:
        let pressed = 0;
        while (pressed = 0) {
            let pressed = Keyboard.keyPressed();
        }

        if ((pressed > 31) & (pressed < 128)) {
            do Output.printChar(pressed);
        }

        // Wait for no key to be pressed:
        let c = pressed;
        while (~(c = 0)) {
            let c = Keyboard.keyPressed();
        }

        return pressed;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String buffer;
        var char c;

        do Output.printString(message);

        let buffer = String.new(64);

        let c = 0;

        while (true) {
            let c = Keyboard.readChar();

            if (c = 128) {  // enter
                do Output.println();
                return buffer;    // Note: the caller is responsible for calling dispose().
            }
            else {
                if (c = 129) {  // backspace
                    if (0 < buffer.length()) {
                        do buffer.eraseLastChar();
                        do Output.backSpace();
                    }
                }
                else {
                    if ((c > 31) & (c < 127)) {
                        do buffer.appendChar(c);
                    }
                }
            }
        }

        return;  // never get here
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String str;

        let str = Keyboard.readLine(message);
        return str.intValue();
    }
}
